/*
  Dynamic Date EBNF
*/



/*
  === start rule ===
*/

DynamicDate   ::= ( Day ( Shift )? ) Sep ( Month ( Shift )? ) Sep ( Year ( Shift )? )


/*
  === rules ===
*/

Day           ::= ( DaySubst | DayNumeral Digit )

Month         ::= ( MonthSubst | MonthNumeral Digit )

Year          ::= ( YearSubst | DoubleDigit DoubleDigit )

Shift         ::= ( Plus | Minus ) DoubleDigit

DoubleDigit   ::= Digit Digit


/*
  === terminal symbols ===
*/

Digit         ::= [0-9]
DayNumeral    ::= [0-3]
MonthNumeral  ::= [0-1]

DaySubst      ::= 'DD'
MonthSubst    ::= 'MM'
YearSubst     ::= 'YYYY'

Sep           ::= '.'
Plus          ::= '+'
Minus         ::= '-'
